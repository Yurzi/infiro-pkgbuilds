# Maintainer: Yurzi <yurzi@foxmail.com>

_extname=pgvector
pkgname=postgresql-${_extname}
pkgver=0.8.0
pkgrel=4
pkgdesc="A vector similarity search extension for PostgreSQL"
arch=('x86_64')
url='https://github.com/pgvector/pgvector'
license=('custom')
depends=('postgresql')
makedepends=('llvm' 'clang')
optdepends=()
_archive="${_extname}-${pkgver}"
source=("${_archive}.tar.gz::https://github.com/pgvector/pgvector/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('867a2c328d4928a5a9d6f052cd3bc78c7d60228a9b914ad32aa3db88e9de27b0')

build() {
  cd "$srcdir/$_archive"
  make OPTFLAGS=""
}

package() {
  cd "$srcdir/$_archive"
  make DESTDIR="$pkgdir/" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

