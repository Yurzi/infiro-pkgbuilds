# Maintainer: Yurzi <yurzi@foxmail.com>

_gitname=plasma-smart-video-wallpaper-reborn
pkgname=plasma6-wallpapers-smart-video-wallpaper-reborn
pkgver=2.6.0
pkgrel=1
pkgdesc="Smart KDE Plasma 6 Wallpaper plugin to play videos on your Desktop/Lock Screen"
arch=('any')
url="https://github.com/luisbocanegra/$_gitname"
license=('GPL3')
depends=('plasma-workspace' 'qt6-multimedia' 'qt6-multimedia-backend')
makedepends=('extra-cmake-modules' 'gettext' 'python')
source=("${_gitname}-${pkgver}.tar.gz::$url/archive/v${pkgver}/${_gitname}-${pkgver}.tar.gz")
sha256sums=('0fb0bd6d155f51afc3db07df7530316df795433de4f2344a10475fad3a349e39')

build() {
	cd "${srcdir}/${_gitname}-$pkgver" || exit
	python ./kpac i18n --no-merge
	cmake -B build -S .
	cmake --build build
}

package() {
	cd "${srcdir}/${_gitname}-$pkgver"
	DESTDIR="$pkgdir" cmake --install build
	chmod 755 "$pkgdir/usr/share/plasma/wallpapers/luisbocanegra.smart.video.wallpaper.reborn/contents/ui/tools/gdbus_get_signal.sh"
}
