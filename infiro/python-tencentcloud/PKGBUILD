# Maintainer: Yurzi <yurzi@foxmail.com>

pkgname='python-tencentcloud'
pkgver=3.0.1454
pkgrel=1
pkgdesc='Tencent Cloud API 3.0 SDK for Python'
url='https://github.com/TencentCloud/tencentcloud-sdk-python'

license=('Apache-2.0')
arch=('any')
depends=(python python-requests python-urllib3 python-certifi)
makedepends=(git python-build python-installer python-setuptools python-wheel)
conflicts=(tencentcloud-sdk-python)
source=(
	"tencentcloud-sdk-python-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
	'01-python3-imports.patch'
)
sha256sums=('0aa1d817e63669e4553a14f92acd6844f2d068e0d522f275677019e9f19d2015'
            'a1ad959180ef49d428e171936ba9c25f4dba937b0588d60d47e1b44003525567')

prepare() {
	cd "tencentcloud-sdk-python-${pkgver}"
	patch -Np1 -i ../01-python3-imports.patch
}

build() {
	cd "tencentcloud-sdk-python-${pkgver}"
	python -m build --wheel --no-isolation
}

check() {
	cd "tencentcloud-sdk-python-${pkgver}"
	PYTHONPATH=${pkgdir}/src python -c "import tencentcloud"
}

package() {
	cd "tencentcloud-sdk-python-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
