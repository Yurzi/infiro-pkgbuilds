# Maintainer: Yurzi <yurzi@foxmail.com>

_extname=VectorChord
pkgname=postgresql-${_extname,,}
pkgver=1.1.0
pkgrel=3
pkgdesc="Scalable, fast, and disk-friendly vector search in Postgres, successor to pgvecto.rs"
arch=('x86_64')
url="https://github.com/tensorchord/VectorChord"
license=('AGPLv3')
depends=(
	'postgresql'
	# 'postgresql-pgvector'
)
provides=("vchord.so=$pkgver")
conflicts=("${_extname,,}-bin" "${_extname,,}" "postgresql-${_extname,,}-immich") # vectorchord-bin does not use 'provides=vectorchord', requires this to resolve
options=(!lto)
makedepends=(
	'rust'      # currently required to strictly control the toolchain as upstream seems to build with nighly versions and cannot currenty be built with stable
	'clang>=16' # upstream builds with clang, claims to work with gcc but does not yet
	# 'gcc>=14' # part of base-devel and hence preferred to clang
)
_archive="${_extname}-${pkgver}"
source=("${_archive}.tar.gz::https://github.com/tensorchord/VectorChord/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('dea94f80844294b4b1731dd9ad8da28b008fda8b36919a34adc75c838de457f2')
install=info.install

prepare() {
	cd "$srcdir/$_archive"
	# pre-download rust dependencies already in prepare()
	cargo fetch --locked --target="$(rustc -vV | sed -n 's/host: //p')"
}
build() {
	cd "$srcdir/$_archive"
	export RUST_BACKTRACE=1 # show backtrace on error
	make build
}

package() {
	cd "$srcdir/$_archive/build/"
	install -Dm 755 "./pkglibdir/vchord.so" "$pkgdir$(pg_config --pkglibdir)/vchord.so"
	install -d "$pkgdir$(pg_config --sharedir)/extension"
	install -Dm 644 ./sharedir/extension/* "$pkgdir$(pg_config --sharedir)/extension/"
}
