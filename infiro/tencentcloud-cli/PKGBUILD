# Maintainer: Yurzi <yurzi@foxmail.com>

pkgname='tencentcloud-cli'
pkgver=3.1.39.1
pkgrel=1
pkgdesc='Tencent Cloud API 3.0 Command Line Interface'
url='https://github.com/TencentCloud/tencentcloud-cli'

_execname='tccli'
license=('Apache-2.0')
arch=('any')
depends=(python python-six python-jmespath python-tencentcloud)
optdepends=()
makedepends=(python-build python-installer python-setuptools python-wheel python-hatchling)
conflicts=('tencentcloud-cli-intl-en')
source=(
	"${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
)
sha256sums=('7bfa05eb1d67a4fc08416264f5e6474bed19bdd0e77827c215edadd52b6a185d')

build() {
	cd "${pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
	cat >completion.bash <<'EOF'
#!/bin/bash
complete -C 'tccli_completer' tccli
EOF
}

package() {
	cd "${pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -Dm644 completion.bash "${pkgdir}/usr/share/bash-completion/completions/${_execname}"
}
