# Maintainer: yurzi <yurzi@foxmail.com>

pkgname=gcli2api-git
_pkgname=gcli2api
pkgver=902.d6eb88a
pkgrel=1
pkgdesc="A server proxy Gemini Cli API to OpenAI compatible API"
arch=('x86_64')
license=('custom')
url="https://github.com/su-kaka/gcli2api"
install=${_pkgname}.install

depends=('python312')
makedepends=('git' 'uv')

_installdir=/opt/${_pkgname}
_venvdir="${_installdir}/venv"

source=(
	"${_pkgname}::git+${url}.git"
	"${_pkgname}.service"
	"${_pkgname}.conf"
)

sha256sums=('SKIP'
	'1100682a01870e89b99650209060f46d9c676b098321434321c88bb24064aaec'
	'6738c1cfd2e639d15913de3cf4a1e47a2696cc891237ee46e0085615843f59d9')

pkgver() {
	cd "${_pkgname}/"
	echo "$(git rev-list --count master).$(git rev-parse --short master)"
}

build() {
	cd "${_pkgname}/"

	# pip install of uv not required because uv is a makedep
	export PYTHONUNBUFFERED=1 # for logging
	uv sync --quiet --no-dev --no-editable --no-progress --python=3.12 --no-managed-python

	# delete any uv bytecode
	find ".venv" -type f -name "*.py[co]" -delete
	find ".venv" -type d -name "__pycache__" -delete

	sed -i "s|${srcdir}/${_pkgname}/.venv|${_venvdir}|g" ".venv/bin/"*
}

package() {
	cd "${_pkgname}/"

	# install app
	install -d "${pkgdir}${_installdir}"
	cp -r ".venv" "${pkgdir}${_installdir}/venv"
	cp -r ./* "${pkgdir}${_installdir}/"

	# install service and conf
	cd "${srcdir}"
	install -Dm644 "${_pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${_pkgname}.service"
	install -Dm644 "${_pkgname}.conf" "${pkgdir}/usr/share/doc/${_pkgname}/${_pkgname}.conf.example"

}
